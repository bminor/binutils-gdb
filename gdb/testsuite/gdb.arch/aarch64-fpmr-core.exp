# Copyright (C) 2018-2025 Free Software Foundation, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# This file is part of the gdb testsuite.

# Test generating and reading a core file with FPMR.

proc check_fpmr_core_file {core_filename} {
    # Load the core file.
    if {
	[gdb_test "core $core_filename" \
	    [multi_line \
		"Core was generated by .*" \
		"Program terminated with signal SIGSEGV, Segmentation fault\\." \
		"#0  ${::hex} in main \\(.*\\) at .*" \
		".* \\*\\(\\(uint64_t \\*\\) crash_address\\) = 0xDEAD.*"] \
	    "load core file"]
    } {
	untested "failed to generate core file"
	return -1
    }

    # Check the value of FPMR in the core file.
    gdb_test "print/x \$fpmr" " = 0x3fff7fc049" \
	     "fpmr contents from core file"
}

require is_aarch64_target
require allow_aarch64_fpmr_tests

standard_testfile
if {[prepare_for_testing "failed to prepare" ${testfile} ${srcfile}]} {
    return -1
}

set binfile [standard_output_file ${testfile}]

if {![runto_main]} {
    return -1
}

set crash_breakpoint "crash point"
gdb_breakpoint [gdb_get_line_number $crash_breakpoint]
gdb_continue_to_breakpoint $crash_breakpoint

gdb_test "print/x \$fpmr" " = 0x3fff7fc049" "fpmr contents from core file"

gdb_test "continue" \
    [multi_line \
	"Program received signal SIGSEGV, Segmentation fault\\." \
	"${::hex} in main \\(\\).* at .*" \
	".* \\*\\(\\(uint64_t \\*\\) crash_address\\) = 0xDEAD.*"] \
	"run to crash"

# Generate the gcore core file.
set gcore_filename [standard_output_file "${testfile}.gcore"]
set gcore_generated [gdb_gcore_cmd "$gcore_filename" "generate gcore file"]

# Generate a native core file.
set core_filename [core_find ${binfile}]
set core_generated [expr {$core_filename != ""}]

# At this point we have a couple core files, the gcore one generated by GDB
# and the native one generated by the Linux Kernel.  Make sure GDB can read
# both correctly.

if {$gcore_generated} {
    clean_restart
    gdb_load ${binfile}
    with_test_prefix "gcore corefile" {
	check_fpmr_core_file $gcore_filename
    }
} else {
    fail "gcore corefile not generated"
}

if {$core_generated} {
    clean_restart
    gdb_load ${binfile}
    with_test_prefix "native corefile" {
	check_fpmr_core_file $core_filename
    }
} else {
    untested "native corefile not generated"
}
