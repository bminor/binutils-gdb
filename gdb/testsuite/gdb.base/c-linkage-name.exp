# Copyright 2018-2020 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# This file is part of the gdb testsuite.  It is intended to test that
# gdb can correctly print arrays with indexes for each element of the
# array.

standard_testfile c-linkage-name.c c-linkage-name-2.c

set sources "${srcdir}/${subdir}/${srcfile} ${srcdir}/${subdir}/${srcfile2}"
if { [gdb_compile "${sources}" "${binfile}" executable {debug}] != "" } {
    untested "failed to compile"
    return -1
}

clean_restart ${binfile}

# Verify that partial symtab expansion for $filename has state $readin

proc verify_psymtab_expanded { filename readin } {
    set cmd "maint info psymtab"
    set test "$cmd: $filename: $readin"
    set re [multi_line \
		"  \{ psymtab \[^\r\n\]*$filename\[^\r\n\]*" \
		"    readin $readin" \
		".*"]

    gdb_test $cmd $re $test
}

# Verify that partial symtab expansion has not taken place for
# c-linkage-name-2.c.

verify_psymtab_expanded c-linkage-name-2.c no

# Try to print MUNDANE, but using its linkage name.

gdb_test "print symada__cS" \
         "'symada__cS' has unknown type; cast it to its declared type" \
         "print symada__cS before partial symtab expansion"

# Force the symbols to be expanded for the unit that contains
# our symada__cS symbol by, e.g. inserting a breakpoint on one
# of the founction also provided by the same using.

gdb_test "break do_something_other_cu" \
         "Breakpoint $decimal at $hex: file .*$srcfile2, line $decimal\\."

# Verify that partial symtab expansion has taken place for
# c-linkage-name-2.c.

verify_psymtab_expanded c-linkage-name-2.c yes

# Flush the symbol cache to prevent the lookup to return the same as before.

gdb_test "maint flush-symbol-cache"

# Try to print MUNDANE using its linkage name again, after partial
# symtab expansion.

gdb_test "print symada__cS" \
         " = {a = 100829103}" \
         "print symada__cS after partial symtab expansion"
