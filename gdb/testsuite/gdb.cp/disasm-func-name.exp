# Copyright 2020-2025 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# This file is part of the gdb testsuite

# Test that the disassembler correctly demangles C++ function names in
# it's header line.

require allow_cplus_tests

standard_testfile .cc

if {[prepare_for_testing "failed to prepare" $testfile \
	 $srcfile {debug c++}]} {
    return -1
}

if {![runto_main]} {
    return
}

proc check_disassembly_header { request expected } {
    gdb_test "disassemble ${request}" \
	"Dump of assembler code for function ${expected}:\r\n.*"
}

gdb_test_no_output "set print asm-demangle on"

check_disassembly_header "main" "main\\(\\)"
check_disassembly_header "process" "process\\(A\\*, int\\)"
check_disassembly_header "A::A" "A::A\\(int\\)"
check_disassembly_header "A::get_i" "A::get_i\\(\\) const"
check_disassembly_header "A::set_i" "A::set_i\\(int\\)"

# Place a breakpoint at STOP_LOC and then continue until a breakpoint
# is hit (we assume this is the breakpoint we just created.
#
# Then disassemble the current function, EXPECT should appear in the
# disassembly header as the name of the current function.
proc continue_and_check { stop_loc expected } {
    gdb_breakpoint $stop_loc
    gdb_continue_to_breakpoint "continue to $stop_loc"

    with_test_prefix "at $stop_loc" {
	check_disassembly_header "" $expected
    }
}

# Initially we are already in main.
check_disassembly_header "" "main\\(\\)"

# Now move forward and check the disassembly at various locations.
continue_and_check "A::A" "A::A\\(int\\)"
continue_and_check "process" "process\\(A\\*, int\\)"
continue_and_check "A::set_i" "A::set_i\\(int\\)"
continue_and_check "A::get_i" "A::get_i\\(\\) const"
